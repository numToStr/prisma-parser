Prisma =
    Datasource
    | Generator
    | Model
    | Enum

Datasource =
    "datasource" Name Object

Generator =
    "generator" Name Object

Model =
    "model" Name Block

Enum =
    "enum" Name Varaints

Object =
    "{" Field+ "}"

Block =
    "{" Column+ "}"

Varaints =
    "{" Name+ "}"

Field =
    Name "=" Expr

Expr =
    Literal | Array | Fn

Fn =
    Name Args

Args =
    "(" Arg ("," Arg)* ")"

Arg =
    Expr | NamedArg

NamedArg =
    Name ":" Expr

Column =
    Name ColumnType Attributes?

ColumnType =
    (Scalar | Ref) Modifier

Modifier =
    "[" "]" | ("?")?

Attribute =
    "@" AttributeName Args?

AttributeName =
    /^[a-zA-Z0-1.]/

Attributes =
    Attribute*

Name =
    /^[a-zA-Z0-1_]/

Literal =
    STRING | NUMBER | BOOLEAN

Scalar =
    "String"
    | "Boolean"
    | "Int"
    | "BigInt"
    | "Float"
    | "Decimal"
    | "DateTime"
    | "Json"
    | "Bytes"
    | "Unsupported" "(" Literal ")"

Ref =
    Name

Array =
    "[" ArrayItem ("," ArrayItem)* "]"

ArrayItem =
    Ref | Literal
